plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.9'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'net.uberfoo.cpm.filesystem.editor'
version '1.0.0'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/Uberfoo-Heavy-Industries/z80utils"
        credentials {
            username = System.getenv("USERNAME")
            password = System.getenv("TOKEN")
        }
    }
}

ext {
    junitVersion = '5.9.1'
}

sourceCompatibility = '19'
targetCompatibility = '19'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'net.uberfoo.cpm.filesystem.editor'
    mainClass = 'net.uberfoo.cpm.filesystem.editor.EditorApp'
}

javafx {
    version = '19'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.base']
}

dependencies {
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation("net.uberfoo.cpm:cpmutils:${cpmutilsVersion}")
    implementation('org.nield:dirtyfx:0.1.2')

    implementation 'org.slf4j:slf4j-api:2.0.6'

    implementation(platform('org.apache.logging.log4j:log4j-bom:2.19.0'))
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'commons-io:commons-io:2.13.0'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'

    implementation 'net.java.dev.jna:jna:5.13.0'
    implementation 'net.java.dev.jna:jna-platform:5.13.0'

    implementation 'org.aerofx:aerofx:0.2'
    implementation 'org.jfxtras:jmetro:11.6.15'

    runtimeOnly 'org.apache.logging.log4j:log4j-core'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/cpm-filesystem-editor-${org.gradle.internal.os.OperatingSystem.current().nativePrefix}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'CPM-Filesystem-Editor-' + org.gradle.internal.os.OperatingSystem.current().nativePrefix
    }
    forceMerge "jackson-core", "jackson-annotations", "jackson-databind", "jackson-dataformat-yaml", "log4j-api", "slf4j-api", "log4j-slf4j2-impl", "dirtyfx"
    addExtraDependencies "javafx"
    mergedModule {
        additive = true
        uses 'org.apache.logging.log4j.util.PropertySource'
        uses 'org.apache.logging.log4j.core.util.ContextDataProvider'
        uses 'org.apache.logging.log4j.core.util.WatchEventService'
        uses 'org.apache.logging.log4j.spi.Provider'
        uses 'org.apache.logging.log4j.message.ThreadDumpMessage.ThreadInfoFactory'
    }
    jpackage {
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
            imageOptions += ['--win-console', '--icon', 'src/main/resources/disc-drive-orange.ico']
        }
        if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            imageOptions += ['--icon', 'src/main/resources/disc-drive-orange-128x128.icns']
        }
    }
}

jlinkZip {
    group = 'distribution'
}